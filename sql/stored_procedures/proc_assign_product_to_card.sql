USE smartov -- EXAMPLE DATABASENAME
GO

IF OBJECT_ID('PROC_ASSIGN_PRODUCT_TO_CARD', 'P') IS NOT NULL -- EXAMPLE PROCEDURE NAME
  DROP PROCEDURE PROC_ASSIGN_PRODUCT_TO_CARD
GO

-- CREATE STORED PROCEDURE
CREATE PROCEDURE PROC_ASSIGN_PRODUCT_TO_CARD -- EXAMPLE NAME
    @reisproductid UNIQUEIDENTIFIER, -- EXAMPLE PARAMETERS
    @kaartid       UNIQUEIDENTIFIER
AS
  DECLARE @TranCounter INT;
  SET @TranCounter = @@TRANCOUNT;
  IF @TranCounter > 0
    SAVE TRANSACTION ProcedureSave;
  ELSE
    BEGIN TRANSACTION;
  BEGIN TRY

  IF NOT EXISTS(SELECT 1
                FROM dbo.KAART
                WHERE KAARTID = @kaartid)
    RAISERROR (56150, 16, 1);

  IF NOT EXISTS(SELECT 1
                FROM dbo.REISPRODUCT
                WHERE REISPRODUCTID = @reisproductid)
    RAISERROR (56151, 16, 1);

  INSERT INTO dbo.PRODUCT_OP_KAART (PRODUCTOPKAARTID, KAARTID, REISPRODUCTID, KOPPELDATUM)
  VALUES (
    NEWID(),
    @kaartid,
    @reisproductid,
    GETDATE()
  )

  IF @TranCounter = 0
    COMMIT TRANSACTION;

  END TRY
  BEGIN CATCH
    IF @TranCounter = 0
      ROLLBACK TRANSACTION;
    ELSE
      IF XACT_STATE() <> -1
        ROLLBACK TRANSACTION ProcedureSave;

    DECLARE @ErrorMessage NVARCHAR(4000);
    DECLARE @ErrorSeverity INT;
    DECLARE @ErrorState INT;
    SELECT @ErrorMessage = ERROR_MESSAGE();
    SELECT @ErrorSeverity = ERROR_SEVERITY();
    SELECT @ErrorState = ERROR_STATE();
    RAISERROR (@ErrorMessage, @ErrorSeverity, @ErrorState);
  END CATCH
