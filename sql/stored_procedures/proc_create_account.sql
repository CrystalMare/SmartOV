USE smartov -- EXAMPLE DATABASENAME
GO

IF OBJECT_ID('PROC_CREATE_ACCOUNT', 'P') IS NOT NULL -- EXAMPLE PROCEDURE NAME
  DROP PROCEDURE PROC_CREATE_ACCOUNT
GO

-- CREATE STORED PROCEDURE
CREATE PROCEDURE PROC_CREATE_ACCOUNT -- EXAMPLE NAME
    @persoonid UNIQUEIDENTIFIER
AS
  DECLARE @TranCounter INT;
  SET @TranCounter = @@TRANCOUNT;
  IF @TranCounter > 0
    SAVE TRANSACTION ProcedureSave;
  ELSE
    BEGIN TRANSACTION;
  BEGIN TRY

  IF NOT EXISTS (SELECT 1 FROM dbo.PERSOON WHERE PERSOONID = @persoonid)
      RAISERROR (56240, 16, 1);

  DECLARE @accountid UNIQUEIDENTIFIER = NEWID();

  INSERT INTO dbo.ACCOUNT (ACCOUNTID, PERSOONID, SALDO)
      OUTPUT INSERTED.ACCOUNTID
      VALUES (
          @accountid,
          @persoonid,
          0
      )

  IF @TranCounter = 0
    COMMIT TRANSACTION;

  END TRY
  BEGIN CATCH
    IF @TranCounter = 0
      ROLLBACK TRANSACTION;
    ELSE
      IF XACT_STATE() <> -1
        ROLLBACK TRANSACTION ProcedureSave;

    DECLARE @ErrorMessage NVARCHAR(4000);
    DECLARE @ErrorSeverity INT;
    DECLARE @ErrorState INT;
    SELECT @ErrorMessage = ERROR_MESSAGE();
    SELECT @ErrorSeverity = ERROR_SEVERITY();
    SELECT @ErrorState = ERROR_STATE();
    RAISERROR (@ErrorMessage, @ErrorSeverity, @ErrorState);
  END CATCH
